plugins {
	id 'org.springframework.boot' version '2.7.3'
	id 'org.springframework.experimental.aot' version '0.12.1'
	id 'io.spring.dependency-management' version '1.0.13.RELEASE'
	id 'java'
	id 'signing'
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

sourceCompatibility = '11'

repositories {
	maven { url 'https://repo.spring.io/release' }
	mavenCentral()
	mavenLocal()
}

ext {
	set('springShellVersion', "2.1.1")
}

dependencies {
	implementation 'org.springframework.shell:spring-shell-starter'
	implementation 'org.springframework.boot:spring-boot-starter-webflux'
	implementation 'org.springdoc:springdoc-openapi-webflux-ui:1.6.11'
	implementation 'org.springframework.boot:spring-boot-starter-logging'

	implementation 'com.bloxbean.cardano:yaci:0.1.8'
	implementation 'com.bloxbean.cardano:cardano-client-lib:0.4.3'
	implementation 'com.bloxbean.cardano:cardano-client-backend:0.4.3'

	implementation 'org.jeasy:easy-rules-core:4.1.0'
	implementation 'org.jeasy:easy-rules-mvel:4.1.0'

	implementation 'org.apache.commons:commons-lang3:3.12.0'
	implementation 'commons-io:commons-io:2.11.0'
	implementation 'com.google.guava:guava:31.1-jre'
	implementation 'org.apache.commons:commons-text:1.9'

	implementation 'org.codehaus.janino:janino:3.1.8'
	compileOnly 'org.projectlombok:lombok'

	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

dependencyManagement {
	imports {
		mavenBom "org.springframework.shell:spring-shell-dependencies:${springShellVersion}"
	}
}

tasks.named('test') {
	useJUnitPlatform()
}

springAot {
	removeXmlSupport = false
	removeSpelSupport = false
	removeYamlSupport = false
}

task genClusterTemplatesZip(type: Zip) {
	from sourceSets.main.resources
	include 'localcluster/**/*'
	archiveName 'localcluster.zip'
	destinationDir(new File("build/resources/main"))
}
generateAot.dependsOn genClusterTemplatesZip

ext.isReleaseVersion = !version.endsWith("SNAPSHOT")
if (isReleaseVersion && !project.hasProperty("skipSigning")) {
	signing {
		sign configurations.archives
	}
}
